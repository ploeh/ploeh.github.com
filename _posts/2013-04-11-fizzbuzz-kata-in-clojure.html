---
layout: post
title: "FizzBuzz kata in Clojure"
description: "This post describes my first experience with doing the FizzBuzz kata in Clojure."
date: 2013-04-11 06:20 UTC
tags: [Languages]
---
{% include JB/setup %}

<div id="post">
	<p>
		<em>{{ page.description }}</em>
	</p>
	<p>
		After having looked at <a href="http://clojure.org/">Clojure</a> for some time, I finally had a bit of time to play with it, so I decided to do the <a href="http://codingdojo.org/cgi-bin/wiki.pl?KataFizzBuzz">FizzBuzz kata</a> in Clojure.
	</p>
	<h3 id="f59b492d7404433fa7c87645f079e6f4">
		Single fizzbuzz function <a href="#f59b492d7404433fa7c87645f079e6f4" title="permalink">#</a>
	</h3>
	<p>
		Clojure has a built-in testing framework, so I wrote a Parameterized Test for a single fizzbuzz function. Using TDD, I added test cases a little at a time, attempting to follow the <a href="http://cleancoder.posterous.com/the-transformation-priority-premise">Transformation Priority Premise</a>, but the end result is this:
	</p>
	<p>
		<pre>(ns fizzbuzz.core-test
  (:use clojure.test
        fizzbuzz.core))

(deftest fizzbuzz-test
  (are [expected i] (= expected (fizzbuzz i))
       "1" 1
       "2" 2
       "Fizz" 3
       "4" 4
       "Buzz" 5
       "Fizz" 6
       "7" 7
       "8" 8
       "Fizz" 9
       "Buzz" 10
       "11" 11
       "Fizz" 12
       "13" 13
       "14" 14
       "FizzBuzz" 15
       "FizzBuzz" 30))</pre>
	</p>
	<p>
		Clojure syntax is somewhat backwards from what I'm normally used to, but the <a href="http://richhickey.github.io/clojure/clojure.test-api.html#clojure.test/are">are</a> macro expands into a collection of tests that each evaluate whether the result of invoking the fizzbuzz function with <code>i</code> is equal to <code>expected</code>.
	</p>
	<p>
		Through a series of transformations of the SUT, I ended up with this implementation of the fizzbuzz function:
	</p>
	<p>
		<pre>(ns fizzbuzz.core)

(defn fizzbuzz [i]
  (cond
    (= 0 (mod i 15)) "FizzBuzz"
    (= 0 (mod i 3)) "Fizz"
    (= 0 (mod i 5)) "Buzz"
    :else (str i)))</pre>
	</p>
	<p>
		This defines a function called <em>fizzbuzz</em> taking a single argument <code>i</code>. The <a href="http://richhickey.github.io/clojure/clojure.core-api.html#clojure.core/cond">cond</a> macro evaluates each test and returns the expression associated with the first test that evauluates to true. The first test checks if <code>i</code> is divisible with 15 and returns "FizzBuzz" if this is the case; the next test checks if <code>i</code> is divisible with 3 and returns "Fizz" if this is true, and so on.
	</p>
	<h3 id="dc83aff6725346a8a2aafd6ce49ba410">
		Printing a range of fizzbuzz values <a href="#dc83aff6725346a8a2aafd6ce49ba410" title="permalink">#</a>
	</h3>
	<p>
		The task defined by the kata is to print all FizzBuzz values from 1 to 100, so the above function is't the final solution. The next step I took was to write a test that defines a version of the fizzbuzz function taking two parameters:
	</p>
	<p>
		<pre>(def acceptance-expected
"1
2
Fizz
4
Buzz
Fizz
7
8
Fizz
Buzz
11
Fizz
13
14
FizzBuzz
16
17
Fizz
19
Buzz
Fizz
22
23
Fizz
Buzz
26
Fizz
28
29
FizzBuzz
31
32
Fizz
34
Buzz
Fizz
37
38
Fizz
Buzz
41
Fizz
43
44
FizzBuzz
46
47
Fizz
49
Buzz
Fizz
52
53
Fizz
Buzz
56
Fizz
58
59
FizzBuzz
61
62
Fizz
64
Buzz
Fizz
67
68
Fizz
Buzz
71
Fizz
73
74
FizzBuzz
76
77
Fizz
79
Buzz
Fizz
82
83
Fizz
Buzz
86
Fizz
88
89
FizzBuzz
91
92
Fizz
94
Buzz
Fizz
97
98
Fizz
Buzz")

(deftest acceptance-test
  (is (= acceptance-expected (fizzbuzz 1 101))))</pre>
	</p>
	<p>
		I decided to define the <code>acceptance-expected</code> value outside of the test case itself, as I thought that made the test a bit more readable. The test case is defined by the <a href="http://richhickey.github.io/clojure/clojure.test-api.html#clojure.test/is">is</a> macro and states that the expected value is <code>acceptance-expected</code> and the actual value is the result of invoking the fizzbuzz function with <em>two</em> arguments: 1 as the (inclusive) start value, and 101 as the (exclusive) end value. The above code listing of fizzbuzz only accept one argument, but the new test case requires two arguments, so I added an overload to the function:
	</p>
	<p>
		<pre>(defn fizzbuzz
  ([i]
    (cond
      (= 0 (mod i 15)) "FizzBuzz"
      (= 0 (mod i 3)) "Fizz"
      (= 0 (mod i 5)) "Buzz"
      :else (str i)))
  ([start end]
    (apply str
      (interpose "\n"
        (map fizzbuzz (range start end))))))</pre>
	</p>
	<p>
		The previous implementation is still there, now contained within the overload taking a single argument <code>i</code>, but now there's also a new overload taking two arguments: <code>start</code> and <code>end</code>.
	</p>
	<p>
		This overload generates a sequence of integers from <code>start</code> to <code>end</code> using the <a href="http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/range">range</a> function. It then maps that sequence of integers into a sequence of strings by <a href="http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/map">mapping</a> each integer to a string with the fizzbuzz function. That gives you a sequence of strings such as <code>("1" "2" "Fizz" "4" "Buzz")</code>.
	</p>
	<p>
		In order to print all the FizzBuzz strings, I need to <a href="http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/interpose">interpose</a> a newline character between each string, which produces a new sequence of strings such as <code>("1" "\n" "2" "\n" "Fizz" "\n" "4" "\n" "Buzz")</code>. To concatenate all these strings, I <a href="http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/apply">apply</a> the <a href="http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/str">str</a> function to the sequence.
	</p>
	<h3 id="2eae1e308c5f401584d617a4f7a9cae8">
		Printing FizzBuzz values from 1 to 100 <a href="#2eae1e308c5f401584d617a4f7a9cae8" title="permalink">#</a>
	</h3>
	<p>
		The requirements of the kata is to print all FizzBuzz values from 1 to 100, and the code already does this. However, I interpret the kata as requiring a single function that takes no parameters, so I added an acceptance test case:
	</p>
	<p>
		<pre>(deftest acceptance-test
  (is (= acceptance-expected (fizzbuzz 1 101)))
  (is (= acceptance-expected (fizzbuzz))))</pre>
	</p>
	<p>
		Notice the second test case in the last line of code that invokes the fizzbuzz function without any parameters. It's easily resolved by adding a third overload:
	</p>
	<p>
		<pre>(defn fizzbuzz
  ([]
    (fizzbuzz 1 101))
  ([i]
    (cond
      (= 0 (mod i 15)) "FizzBuzz"
      (= 0 (mod i 3)) "Fizz"
      (= 0 (mod i 5)) "Buzz"
      :else (str i)))
  ([start end]
    (apply str
      (interpose "\n"
        (map fizzbuzz (range start end))))))</pre>
	</p>
	<p>
		As you can see, the first overload takes no parameters and simply invokes the previously desribed overload with the <code>start</code> and <code>end</code> arguments.
	</p>
	<p>
		FWIW, this entire solution is structurally similar to <a href="/2012/07/20/FizzBuzzkatainFstage1/">my implementation of FizzBuzz in F#</a>.
	</p>
</div>